(*
    --- Day 5: Supply Stacks ---
The expedition can depart as soon as the final supplies have been unloaded from the ships. Supplies are stored in stacks of marked crates, but because the needed supplies are buried under many other crates, the crates need to be rearranged.

The ship has a giant cargo crane capable of moving crates between stacks. To ensure none of the crates get crushed or fall over, the crane operator will rearrange them in a series of carefully-planned steps. After the crates are rearranged, the desired crates will be at the top of each stack.

The Elves don't want to interrupt the crane operator during this delicate procedure, but they forgot to ask her which crate will end up where, and they want to be ready to unload them as soon as possible so they can embark.

They do, however, have a drawing of the starting stacks of crates and the rearrangement procedure (your puzzle input). For example:

    [D]    
[N] [C]    
[Z] [M] [P]
 1   2   3 

move 1 from 2 to 1
move 3 from 1 to 3
move 2 from 2 to 1
move 1 from 1 to 2
In this example, there are three stacks of crates. Stack 1 contains two crates: crate Z is on the bottom, and crate N is on top. Stack 2 contains three crates; from bottom to top, they are crates M, C, and D. Finally, stack 3 contains a single crate, P.

Then, the rearrangement procedure is given. In each step of the procedure, a quantity of crates is moved from one stack to a different stack. In the first step of the above rearrangement procedure, one crate is moved from stack 2 to stack 1, resulting in this configuration:

[D]        
[N] [C]    
[Z] [M] [P]
 1   2   3 
In the second step, three crates are moved from stack 1 to stack 3. Crates are moved one at a time, so the first crate to be moved (D) ends up below the second and third crates:

        [Z]
        [N]
    [C] [D]
    [M] [P]
 1   2   3
Then, both crates are moved from stack 2 to stack 1. Again, because crates are moved one at a time, crate C ends up below crate M:

        [Z]
        [N]
[M]     [D]
[C]     [P]
 1   2   3
Finally, one crate is moved from stack 1 to stack 2:

        [Z]
        [N]
        [D]
[C] [M] [P]
 1   2   3
The Elves just need to know which crate will end up on top of each stack; in this example, the top crates are C in stack 1, M in stack 2, and Z in stack 3, so you should combine these together and give the Elves the message CMZ.

After the rearrangement procedure completes, what crate ends up on top of each stack?
*)

module Day5

open Utility

type State = char array array

let parseCrates =
    dropLast
    >> Seq.transpose // spin baby, spin
    >> Seq.indexed // get the keys
    >> Seq.filter (fst >> (fun key -> key % 4 = 1)) // the numbers are 4 characters apart
    >> Seq.map (snd >> Seq.filter (notEqualTo ' ') >> Seq.toArray) // filter out empty spaces
    >> Seq.toArray // Arrays cuz we're gonna be mutating by index

let parseProcedure =
    split ("movefromto []".ToCharArray(0, 13)) // split on non number chars
    >> Seq.filter (notEqualTo "") // remove empty strings
    >> Seq.map int // make em ints!
    >> Seq.toList // lists for deconstruction

// more filtering empty values because meh.
let parseProcedures = Seq.map parseProcedure >> (Seq.filter (isnt Seq.isEmpty))

let startsWithM =
    Seq.tryHead >> Option.map (notEqualTo 'm') >> Option.defaultValue false

let parseInput =
    // split on first line that starts with 'm'
    splitBy startsWithM >> tupleMap parseCrates parseProcedures

// REDUCERS
let folder order (crates: State) (amt :: from :: to_ :: _) =
    let cratesToMove = Array.take amt crates[from - 1] |> order

    crates[from - 1] <- Array.skip amt crates[from - 1]
    crates[to_ - 1] <- Array.append cratesToMove crates[to_ - 1]

    crates

// put it all together
let solve order =
    parseInput
    >> unpack (Seq.fold (folder order))
    >> Array.choose Array.tryHead
    >> System.String

let part1 = solve Array.rev


(*
    --- Part Two ---
As you watch the crane operator expertly rearrange the crates, you notice the process isn't following your prediction.

Some mud was covering the writing on the side of the crane, and you quickly wipe it away. The crane isn't a CrateMover 9000 - it's a CrateMover 9001.

The CrateMover 9001 is notable for many new and exciting features: air conditioning, leather seats, an extra cup holder, and the ability to pick up and move multiple crates at once.

Again considering the example above, the crates begin in the same configuration:

    [D]    
[N] [C]    
[Z] [M] [P]
 1   2   3 
Moving a single crate from stack 2 to stack 1 behaves the same as before:

[D]        
[N] [C]    
[Z] [M] [P]
 1   2   3 
However, the action of moving three crates from stack 1 to stack 3 means that those three moved crates stay in the same order, resulting in this new configuration:

        [D]
        [N]
    [C] [Z]
    [M] [P]
 1   2   3
Next, as both crates are moved from stack 2 to stack 1, they retain their order as well:

        [D]
        [N]
[C]     [Z]
[M]     [P]
 1   2   3
Finally, a single crate is still moved from stack 1 to stack 2, but now it's crate C that gets moved:

        [D]
        [N]
        [Z]
[M] [C] [P]
 1   2   3
In this example, the CrateMover 9001 has put the crates in a totally different order: MCD.

Before the rearrangement process finishes, update your simulation so that the Elves know where they should stand to be ready to unload the final supplies. After the rearrangement procedure completes, what crate ends up on top of each stack?
*)

let part2 = solve id

let solution (input: string seq) =
    { Part1 = part1 input |> string
      Part2 = part2 input |> string }
